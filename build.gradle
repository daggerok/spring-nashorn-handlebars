plugins {
  id "java"
  id "idea"
  id "eclipse"
  id "org.springframework.boot" version "1.5.8.RELEASE"
  id "com.moowork.node" version "1.2.0"
}

group "daggerok"
version "0.0.1"
sourceCompatibility = targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compileOnly("org.projectlombok:lombok")
  compile("org.springframework.boot:spring-boot-starter-mustache")
  runtime("org.springframework.boot:spring-boot-devtools")
  testCompile("org.springframework.boot:spring-boot-starter-test")
}

node {
  download = true
  version = "8.9.0"
  npmVersion = "5.5.1"
  yarnVersion = "v0.24.5"
  nodeModulesDir = file("$project.projectDir")
}

clean.doFirst {
  ["node_modules", "build", "dist", "out", "src/main/resources/vendors"].each {
    delete "$projectDir/$it"
  }
}

assemble.dependsOn yarn_install

eclipse {
  classpath {
    containers.remove("org.eclipse.jdt.launching.JRE_CONTAINER")
    containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8"
  }
}

task wrapper(type: Wrapper) { gradleVersion = "4.3" }
